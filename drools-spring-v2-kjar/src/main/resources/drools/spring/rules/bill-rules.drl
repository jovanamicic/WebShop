package drools.spring.rules

import drools.spring.example.model.Bill;
import drools.spring.example.model.Limit;
import drools.spring.example.model.DiscountBill;

import java.util.Date;
import java.util.Calendar;

//Osnovni popust od 5% na ceo račun ukoliko ukupna vrednost naručenih artikala prelazi 200000 dinara.
rule "Rule h)"
	agenda-group "discountBill"
    when
        $b: Bill(originalTotalPrice > 200000)
    then
    	DiscountBill discount = new DiscountBill();
    	discount.setBasic(true);
    	discount.setDiscount(5);
    	discount.setBill($b);
    	$b.getDiscountsBill().add(discount);
    	System.out.println("BILL DISCOUNT h");
end


function Date todayTwoYears() {
    Calendar cal = Calendar.getInstance();
	cal.add(Calendar.YEAR, -2); 
	Date retVal = cal.getTime();
    return retVal;
}

//Dodatni popust od 1% na ceo račun ukoliko je kupac korisnik sistema više od 2 godine (nagrađuju stari kupci).
rule "Rule i)"
	agenda-group "discountBill"
    when
        $b: Bill(customer.regDate.compareTo(todayTwoYears()) <= 0)
    then
    	DiscountBill discount = new DiscountBill();
    	discount.setBasic(false);
    	discount.setDiscount(1);
    	discount.setBill($b);
    	$b.getDiscountsBill().add(discount);
    	System.out.println("Rule i)");
end

//Dodatni popust od 1% na ceo račun ukoliko je kupac pripada kategoriji srebrni kupci ili zlatni kupci (nagrađuju kupci sa posebnim privilegijama).
rule "Rule j)"
	agenda-group "discountBill"
    when
        $b: Bill(customer.ccategory.name == "Silver" || customer.ccategory.name == "Gold")
    then
    	DiscountBill discount = new DiscountBill();
    	discount.setBasic(false);
    	discount.setDiscount(1);
    	discount.setBill($b);
    	$b.getDiscountsBill().add(discount);
    	System.out.println("BILL DISCOUNT j");
end

// Dodatni popust od 3% na ceo račun ukoliko ukupna vrednost naručenih artikala prelazi
// 50000 dinara i ukoliko u računu postoje barem 10 artikala čija ukupna cena prelazi 50% cene
// ukupne vrednosti naručenih artikala.
rule "Rule k)"
	agenda-group "discountBill"
    when
        $b: Bill( originalTotalPrice > 50000, items.size() >= 10)
    then
    	DiscountBill discount = new DiscountBill();
    	discount.setBasic(false);
    	discount.setDiscount(3);
    	discount.setBill($b);
    	$b.getDiscountsBill().add(discount);
    	System.out.println("Rule k)");
end

//	1. Za račun saberi sve popuste (osnovni + svi dodatni).
//	2. Potom se rezultat sabiranja popusta (izražen u procentima) upiše u polje procenat umanjenja
//		za račun.
//	3. Na osnovu procenata umanjenja računa i konačne cene za svaku stavku računa, izračunaj i
//		upiši konačnu cenu računa. Cena se izračunava tako što se saberu sve konačne cene za svaku
//		stavku, te se na dobijeni zbir primeni procenat umanjenja za račun.

rule "Rule l"
	agenda-group "discountBillFinal"
    when
		$b: Bill($discounts : discountsBill, $items: items)
		
		$total : Number() 
             from accumulate( DiscountBill($discount : discount) from $discounts,
                              sum( $discount ) )
                              
        $finalPriceSum : Number()
        	from accumulate( Item($price : finalPrice) from $items,
                              sum( $price) )
    then
    	$b.setDiscount($total.intValue());
    	$b.setFinalPrice($finalPriceSum.doubleValue() * ( 1 - (($b.getDiscount() * 1.0) / 100) ) );
    	System.out.println("Rule l)");
end


